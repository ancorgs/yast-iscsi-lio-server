/*
|***************************************************************************
|
| Copyright (c) [2012] Novell, Inc.
| All Rights Reserved.
|
| This program is free software; you can redistribute it and/or
| modify it under the terms of version 2 of the GNU General Public License as
| published by the Free Software Foundation.
|
| This program is distributed in the hope that it will be useful,
| but WITHOUT ANY WARRANTY; without even the implied warranty of
| MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.   See the
| GNU General Public License for more details.
|
| You should have received a copy of the GNU General Public License
| along with this program; if not, contact Novell, Inc.
|
| To contact Novell about this file by physical or electronic mail,
| you may find current contact information at www.novell.com
|
|***************************************************************************
*/
{

textdomain "iscsi-lio-server";
import "IscsiServerFunctions";
import "IscsiLioData";
import "Label";
import "Event";
import "ContextMenu";
import "IP";
//	**************** global funcions and variables *****
string curr_target = "";
integer curr_tpg = 1;
map<string, map<integer,integer> > changed_lun = $[];
map<string, map > changed_auth = $[];
list<string> del_clnt = [];

// set incoming authentication enabled/disabled status
void SetAuthIn(boolean status)
    {
    y2milestone("Status of AuthIncoming %1", status);
    UI::ChangeWidget(`id(`user_in),`Enabled, status );
    UI::ChangeWidget(`id(`pass_in),`Enabled, status );
    UI::ChangeWidget(`id(`auth_in),`Value, status );
    if(status) UI::ChangeWidget(`id(`auth_none),`Value, !status );
    }

// set outgoing authentication enabled/disabled status
void SetAuthOut(boolean status)
    {
    y2milestone("Status of AuthOutgoing %1", status);
    UI::ChangeWidget(`id(`user_out),`Enabled, status );
    UI::ChangeWidget(`id(`pass_out),`Enabled, status );
    UI::ChangeWidget(`id(`auth_out),`Value, status );
    if(status) UI::ChangeWidget(`id(`auth_none),`Value, !status );
    }

// get values for incoming authentication
list<string> GetIncomingValues()
    {
    list<string> values = [];
    if ((boolean)UI::QueryWidget(`id(`auth_in), `Value) == true)
        values = [ (string)UI::QueryWidget(`id(`user_in), `Value), 
                   (string)UI::QueryWidget(`id(`pass_in), `Value) ];
    return values;
    }

// get values for outgoing authentication
list<string> GetOutgoingValues()
    {
    list<string> values = [];
    if ((boolean)UI::QueryWidget(`id(`auth_out), `Value) == true)
        values = [ (string)UI::QueryWidget(`id(`user_out), `Value), 
                   (string)UI::QueryWidget(`id(`pass_out), `Value) ];
    return values;
    }

//	**************** Global Dialog	*********************
void initGlobalValues(map auth)
    {
    string user = "";
    string pass = "";
    // incoming authentication
    if( !isempty(auth["incoming"]:[]) )
        {
        user = auth["incoming",0]:"";
        pass = auth["incoming",1]:"";
        }
    UI::ChangeWidget(`id(`user_in), `Value, user );
    UI::ChangeWidget(`id(`pass_in), `Value, pass );
    SetAuthIn(!isempty(auth["incoming"]:[]));
    // outgoing authentication
    user = "";
    pass = "";
    if( !isempty(auth["outgoing"]:[]) )
        {
        user = auth["outgoing",0]:"";
        pass = auth["outgoing",1]:"";
        }
    UI::ChangeWidget(`id(`user_out), `Value, user );
    UI::ChangeWidget(`id(`pass_out), `Value, pass );
    SetAuthOut(!isempty(auth["outgoing"]:[]));
    }

// initialize discovery authentication or authentication for given target
void initGlobal (string key)
    {
    initGlobalValues(IscsiLioData::GetAuth("",0,""));
    }

// save discovery authentication or authentication for given target
void storeGlobal(string option_id, map option_map)
    {
    y2milestone( "storeGlobal id:%1 map:%2", option_id, option_map );
    boolean ret = false;
    ret = IscsiLioData::SetAuth( "", 0, "", GetIncomingValues(), GetOutgoingValues() );
    if( !ret )
        {
        Popup::Error( _("Problem changing authentication"));
        }
    IscsiLioData::UpdateConfig();
    }

// validate functions checks the secret for incoming and outgoing cannot be same
boolean validateGlobal(string key, map event)
    {
    y2milestone( "validateGlobal key:%1 ev:%2", key, event );
    boolean ret=true;
    list<string> in = GetIncomingValues();
    if( size(in)>0 && (size(in[0]:"")==0 || size(in[1]:"")==0) )
        {
        boolean user_fail = size(in[0]:"")==0;
        string txt = user_fail ? _("Invalid Username") : _("Invalid Password.");
        UI::SetFocus(`id(user_fail?`user_in:`pass_in));
        Popup::Error( txt );
        ret = false;
        }
    list<string> out = GetOutgoingValues();
    if( size(out)>0 && (size(out[0]:"")==0 || size(out[1]:"")==0) )
        {
        boolean user_fail = size(out[0]:"")==0;
        string txt = user_fail ? _("Invalid Username") : _("Invalid Password.");
        UI::SetFocus(`id(user_fail?`user_out:`pass_out));
        Popup::Error( txt );
        ret = false;
        }
    return ret;
    }

//	**************** Target Auth	*******************
// handle authentication dialog
symbol handleAuth(string key, map event)
    {
    if (event["EventReason"]:"" == "ValueChanged")
        {
        boolean status = false;
        // enable/disable none/incoming/outgoing authentication
        switch((symbol)event["ID"]:nil)
            {
            case(`auth_none):
                status = (boolean)UI::QueryWidget(`id(`auth_none), `Value);
                SetAuthIn(!status);
                SetAuthOut(!status);
                break;
            case(`auth_in):
                status = (boolean)UI::QueryWidget(`id(`auth_in), `Value);
                SetAuthIn(status);
                break;
            case(`auth_out):
                status = (boolean)UI::QueryWidget(`id(`auth_out), `Value);
                SetAuthOut(status);
                break;
            }
        }
    return nil;
    }

term AuthTerm()
    {
    term t =
        `VBox(
            `Left( `CheckBox(`id(`auth_none),`opt(`notify), _("No Authentication"), true) ),
            `VSpacing(1.5),
            `Left( `CheckBox(`id(`auth_in),`opt(`notify), _("Incoming Authentication"), false) ),
            `HBox(
                `InputField(`id(`user_in), `opt(`hstretch), _("Username")), 
                `Password(`id(`pass_in), _("Password"))),
            `VSpacing(1.5),
            `Left( `CheckBox(`id(`auth_out),`opt(`notify), _("Outgoing Authentication"), false) ),
            `HBox(
                `InputField(`id(`user_out), `opt(`hstretch), _("Username")), 
                `Password(`id(`pass_out), _("Password")))
            );
    return( t );
    }

boolean CheckLun( integer l, list<term> other, boolean silent )
    {
    string s = tostring(l);
    boolean ret = isempty(filter( term i, other, ``(i[1]:""==s)));
    y2milestone( "CheckLun other:%1", other );
    y2milestone( "CheckLun l:%1 ret:%2", l, ret );
    if( !ret && !silent )
        Popup::Error( _("Selected Lun is already in use!") );
    return( ret );
    }

boolean CheckName( string n, list<term> other )
    {
    boolean ret = isempty(filter( term i, other, ``(i[2]:""==n)));
    if( !ret )
        Popup::Error( _("Selected Name is already in use!") );
    return( ret );
    }

boolean CheckPath( string p, list<term> other )
    {
    boolean ret = IscsiLioData::CheckPath( p )[0]:false;
    if( !ret )
        Popup::Error( _("Selected Path must be either block device or normal file!") );
    if( ret && !isempty(filter( term i, other, ``(i[3]:""==p))) )
        {
        Popup::Error( _("Selected Path is already in use!") );
        ret = false;
        }
    return( ret );
    }

term LUNDetailDialog( integer pos, list<term> items )
    {
    y2milestone( "LUNDetailDialog pos:%1 items:%2", pos, items );
    list<term> other = (pos>=0)?remove(items,pos):items;
    y2milestone( "LUNDetailDialog other:%1", other );
    term previous= items[pos]:`Empty();
    term ret = `Empty();
    string lun_def = "99";
    if( pos<0 )
        {
        integer count=0;
        while( !CheckLun(count,other,true) )
            {
            count = count+1;
            }
        lun_def = tostring(count);
        }
    term lun_dialog =
        `VBox(
            `Left( `InputField( `id( `lun ), `opt(`hstretch), _("LUN"), previous[1]:lun_def ) ),
            `VSpacing(1),
            `HBox(
                `InputField( `id( `path ), `opt(`hstretch), _("Path:"), previous[3]:""),
                `VBox( `Label(""), `PushButton(`id(`browse), _("Browse")))),
            `InputField( `id( `name ), `opt(`hstretch), "Name (autogenerated when empty):", previous[2]:"" ),
            `VSpacing(1),
            `ButtonBox( 
                `PushButton(`id(`ok), `opt(`default), Label::OKButton() ), 
                `PushButton(`id(`cancel), Label::CancelButton() ))
            );
    UI::OpenDialog(lun_dialog);
    UI::ChangeWidget(`id(`lun), `ValidChars, "0123456789" );
    symbol sym = `nil;
    while(sym != `ok && sym !=`cancel)
        {
        sym = (symbol)UI::UserInput();
        if(sym==`browse)
            {
            string file = UI::AskForExistingFile("/", "", _("Select file or device"));
            if (file!=nil && CheckPath( file, other ) )
                UI::ChangeWidget(`path, `Value, file);
            }
        else if( sym==`path )
            {
            CheckPath( (string)UI::QueryWidget(`path,`Value), other );
            }
        if(sym == `ok) 
            {
            integer lun=tointeger(UI::QueryWidget(`lun, `Value));
            string name = (string)UI::QueryWidget(`name,`Value);
            string pth = (string)UI::QueryWidget(`path,`Value);
            y2milestone( "LUNDetailDialog lun:%1 name:%2 path:%3", lun, name, pth );
            if( !CheckPath( pth, other ) || !CheckLun( lun, other, false ) ||
                !CheckName( name, other ))
                sym = `again;
            if( sym==`ok )
                {
                if( isempty(name) )
                    {
                    list<string> used = maplist( term i, other, ``(i[2]:""));
                    y2milestone( "LUNDetailDialog used:%1", used );
                    name = IscsiLioData::CreateLunName( used, pth );
                    }
                ret = `item( `id(pos>=0?pos:size(other)), lun, name, pth );
                y2milestone( "LUNDetailDialog ret:%1", ret );
                }
            }
        }
    UI::CloseDialog();
    y2milestone( "LUNDetailDialog ret:%1", ret );
    return ret;
    }

integer TpgIdFromTpgItem( string s )
    {
    integer val = (s!="-")?tointeger(s):-1;
    if( val==nil )
        val = -1;
    return( val );
    }

string TpgItemFromTpgId( integer i )
    {
    return( (i>=0) ? tostring(i) : "-" );
    }

void ChangeTpgItems( integer id, integer value, list<term>& items )
    {
    y2milestone( "ChangeTpgItems id:%1 value:%2", id, value );
    if( value!=nil )
        {
        term it = items[id]:`Empty();
        it[2] = TpgItemFromTpgId( value );
        items[id] = it;
        UI::ChangeWidget(`id(`lun), `Items, items );
        UI::ChangeWidget(`id(`lun), `CurrentItem, id );
        }
    }

map<integer,integer> LUNMapDialog( string clnt )
    {
    y2milestone( "LUNMapDialog clnt:%1", clnt );
    map<integer,integer> lmap = $[];
    if( haskey(changed_lun,clnt) )
        lmap = changed_lun[clnt]:$[];
    else
        lmap = IscsiLioData::GetClntLun(curr_target,curr_tpg,clnt); 
    y2milestone( "LUNMapDialog map:%1", lmap );
    list<integer> ll = maplist( integer l, integer d, lmap, ``(l));
    list<term> lt = maplist( integer l, map d, IscsiLioData::GetLun(curr_target,curr_tpg), 
                             ``(`item(`id(l),tostring(l))));
    lt = add( lt, `item(`id(-1),"-"));
    integer mx = ll[size(ll)-1]:-1;
    integer i = 0;
    list<term> items = [];
    while( i<=mx )
        {
        items = add( items, `item( `id(i), tostring(i), haskey(lmap,i)?tostring(lmap[i]:99):"-" ));
        i=i+1;
        }
    y2milestone( "LUNMapDialog items:%1", items );
    term lun_dialog =
        `VBox(
            `MinHeight( 10, `Table(`id(`lun), `opt(`keepSorting,`immediate,`notify,`notifyContextMenu),
                                   `header(_("Client Lun"), _("Target LUN")),
                                   items )),
            `Left( `HBox( 
                `PushButton(`id(`add), _("Add") ),
                `PushButton(`id(`delete), _("Delete") ),
                `Label(_("Change:")), `ComboBox(`id(`change), `opt(`notify), "", lt ))),
            `ButtonBox( 
                `PushButton(`id(`ok), `opt(`default), Label::OKButton() ), 
                `PushButton(`id(`cancel), Label::CancelButton() ))
            );
    UI::OpenDialog(lun_dialog);
    if( size(items)==0 )
        {
        UI::ChangeWidget(`id(`change), `Enabled, false );
        UI::ChangeWidget(`id(`delete), `Enabled, false );
        }
    UI::ChangeWidget(`id(`change), `Value, TpgIdFromTpgItem(items[0,2]:"-") );
    symbol sym = `nil;
    while(sym != `ok && sym !=`cancel)
        {
        map ev = UI::WaitForEvent();
        sym = Event::IsWidgetActivatedOrSelectionChanged(ev);
        if( sym==nil )
            sym = Event::IsWidgetValueChanged(ev);
        if( sym==nil )
            sym = Event::IsWidgetContextMenuActivated(ev);
        y2milestone( "LUNMapDialog event:%1 sym:%2", ev, sym );
        if(sym==`lun)
            {
            integer id = (integer)UI::QueryWidget(`lun,`CurrentItem);
            y2milestone( "LUNMapDialog id:%1 item:%2", id, items[id]:nil );
            if( Event::IsWidgetContextMenuActivated(ev)!=nil )
                {
                UI::OpenContextMenu(`menu(lt));
                integer value = (integer)UI::UserInput();
                ChangeTpgItems( id, value, items );
                UI::ChangeWidget(`id(`change), `Value, value );
                }
            else
                {
                UI::ChangeWidget(`id(`change), `Value, TpgIdFromTpgItem(items[id,2]:"-") );
                }
            }
        else if( sym==`add )
            {
            integer n = size(items);
            items = add( items, `item( `id(n), tostring(n), "-" ));
            UI::ChangeWidget(`id(`lun), `Items, items );
            UI::ChangeWidget(`id(`lun), `CurrentItem, n );
            UI::ChangeWidget(`id(`change), `Value, -1 );
            if( size(items)==1 )
                {
                UI::ChangeWidget(`id(`change), `Enabled, true );
                UI::ChangeWidget(`id(`delete), `Enabled, true );
                }
            }
        else if( sym==`delete )
            {
            integer id = (integer)UI::QueryWidget(`lun,`CurrentItem);
            if( id!=nil && id<size(items) )
                {
                ChangeTpgItems( id, -1, items );
                }
            }
        else if( sym==`change )
            {
            integer id = (integer)UI::QueryWidget(`lun,`CurrentItem);
            integer value = (integer)UI::QueryWidget(`change,`Value);
            if( id!=nil && id<size(items) )
                {
                ChangeTpgItems( id, value, items );
                }
            }
        else if(sym == `ok) 
            {
            lmap = $[];
            foreach( term it, items,
                {
                string s = it[2]:"-";
                i = nil;
                if( s!="-" )
                    i = tointeger(it[2]:"");
                if( i!=nil )
                    lmap[tointeger(it[1]:"0")] = i;
                });
            y2milestone( "LUNMapDialog ret:%1", lmap );
            boolean ok = true;
            list<integer> ll = maplist( integer l, integer d, lmap, ``(l));
            list<integer> ld = maplist( integer l, integer d, lmap, ``(d));
            i=0;
            while( i<size(ll) && ok )
                {
                ok = size(filter( integer j, ld, ``(j==ld[i]:-1)))<=1;
                if( !ok )
                    {
                    string txt = sformat(_("Target LUN %1 used more than once!"), ld[i]:-1 );
                    integer j=i+1;
                    while(ld[i]:-1!=ld[j]:-1 && j<size(ll))
                        j=j+1;
                    if( j<size(items) )
                        UI::ChangeWidget(`id(`lun), `CurrentItem, ll[j]:0 );
                    Popup::Error( txt );
                    }
                i=i+1;
                };
            if( !ok )
                sym = `again;
            }
        else if(sym == `cancel) 
            lmap = nil;
        }
    UI::CloseDialog();
    if( lmap!=nil )
        changed_lun[clnt] = lmap;
    y2milestone( "LUNMapDialog ret:%1", lmap );
    return lmap;
    }

map ClntAuthDialog( string clnt )
    {
    y2milestone( "ClntAuthDialog clnt:%1", clnt );
    map lmap = $[];
    if( haskey(changed_auth,clnt) )
        lmap = changed_auth[clnt]:$[];
    else
        lmap = IscsiLioData::GetAuth(curr_target,curr_tpg,clnt); 
    y2milestone( "ClntAuthDialog map:%1", lmap );
    term auth_dialog = `VBox( `MarginBox( 6, 2, AuthTerm() ),
                              `ButtonBox(
                                  `PushButton(`id(`ok), `opt(`default), Label::OKButton() ),
                                  `PushButton(`id(`cancel), Label::CancelButton() ))
                            );
    UI::OpenDialog(auth_dialog);
    initGlobalValues( lmap );
    symbol sym = `nil;
    while(sym != `ok && sym !=`cancel)
        {
        map ev = UI::WaitForEvent();
        sym = Event::IsWidgetActivatedOrSelectionChanged(ev);
        if( sym==nil )
            sym = Event::IsWidgetValueChanged(ev);
        y2milestone( "ClntAuthDialog event:%1 sym:%2", ev, sym );
        if( sym!=nil )
            handleAuth( "", ev );
        if(sym == `ok) 
            {
            lmap = $[];
            if( !validateGlobal( "", $[] ))
                sym = `again;
            else
                {
                list in = GetIncomingValues();
                list out = GetOutgoingValues();
                if( size(in)>0 )
                    lmap["incoming"] = in;
                if( size(out)>0 )
                    lmap["outgoing"] = out;
                }
            }
        else if(sym == `cancel) 
            lmap = nil;
        }
    UI::CloseDialog();
    if( lmap!=nil )
        changed_auth[clnt] = lmap;
    y2milestone( "ClntAuthDialog ret:%1", lmap );
    return lmap;
    }

// dialog to add/modify user and password
list <string> getDialogValues(string user, string pass){
 UI::OpenDialog( `VBox(
		   `InputField(`id(`p_user), `opt(`hstretch), _("Username"), user),
		   `Password(`id(`p_pass), _("Password"), pass),
		   `HBox(
			`PushButton(`id(`ok), _("OK")),
			`PushButton(`id(`cancel),_("Cancel")))
			)
                );
	boolean cycle = true;
	while(cycle){
	switch((symbol) UI::UserInput()){
	 case(`ok):
		user = tostring( UI::QueryWidget(`id(`p_user), `Value) );
		pass = tostring( UI::QueryWidget(`id(`p_pass), `Value) );
		UI::CloseDialog();
		cycle = false;
		break;
	 case(`cancel):
		cycle = false;
		UI::CloseDialog();
		break;
	 }
	}
 if (!isempty(user) && !isempty(pass)) return [user, pass];
	else return [];
}


symbol saveConfiguration(string key, map event)
    {
    y2milestone( "saveConfiguration key:%1 event:%2", key, event );
    if( is(event["ID"]:nil, string) && event["ID"]:"" == "save")
        {
        any filename = UI::AskForSaveFileName("/",  "*", _("Save as..."));
        if ( filename != nil && (string)filename!="")
            {
            if( IscsiServerFunctions::SaveIntoFile( (string)filename ))
                {
                Popup::Message(sformat(_("File %1 was saved successfully."), filename));
                list pathComponents = splitstring(  (string)filename, "/");
                integer s = size(pathComponents) - 1;
                string base = pathComponents[s]:"default";
                }
            else
                {
                Popup::Warning(_("An error occurred while saving the file."));
                }
            }
        }
    return nil;
    }

list<term> RemoveById( list<term> it, any id )
    {
    y2milestone( "RemoveById id:%1 item:%2", id, filter( term i, it, ``(i[0,0]:(any)99==id)) );
    return( filter( term i, it, ``(i[0,0]:(any)99!=id)) );
    }

term GetById( list<term> it, any id )
    {
    y2milestone( "GetById id:%1", id );
    term t = filter( term i, it, ``(i[0,0]:(any)99==id))[0]:`Empty();
    return( t );
    }

//	**************** Server Dialog	*********************
// dialog with targets

// initialize target dialog
void initTable (string key) ``{
    integer count = 0;
    list <term> inc_items = [];
    // create items from targets
    list<list> tgt = IscsiLioData::GetTargets();
    foreach(list l, tgt, 
        {
        inc_items = add(inc_items, `item(`id(count), l[0]:"", tostring(l[1]:0)));
        count = count + 1;
        });
    // put it into table
    UI::ChangeWidget(`id(`server), `Items, inc_items);
    }

symbol handleTable (string table, map event)
    {
    symbol ret = nil;
    if(event["EventReason"]:"" == "Activated")
        {
        switch((symbol)event["ID"]:nil)
            {
            case(`add):
                // goto  AddDialog() (initAddTarget)
                ret = `add;
                break;
            case(`delete):
                // remove a item
                any del = UI::QueryWidget(`id(`server),`CurrentItem);
                y2milestone("handleTable del:%1",del);
                if (del != nil)
                    {
                    if(Popup::ContinueCancel(_("Really delete the selected item?")))
                        {
                        list<term> it=(list<term>)UI::QueryWidget(`id(`server), `Items);
                        term i = GetById( it, del );
                        y2milestone("handleTable item:%1",i);
                        if( IscsiLioData::DelTarget( i[1]:"", tointeger(i[2]:"-1") ))
                            {
                            it = RemoveById( it, del );
                            UI::ChangeWidget(`id(`server), `Items, it);
                            IscsiLioData::UpdateConfig();
                            }
                        } 
                    else 
                        y2milestone("handleTable Delete canceled");
                    }
                break;
            case(`edit):
                // edit new item
                integer edit=tointeger( UI::QueryWidget(`id(`server), `CurrentItem) );
                term t = (term)UI::QueryWidget(`id(`server), `Item(edit));
                y2milestone("handleTable num:%1 t:%2", edit, t);
                curr_target = t[1]:"";
                curr_tpg = tointeger(t[2]:"");
                y2milestone("handleTable tgt:%1 tpg:%2", curr_target, curr_tpg );
                if (IscsiServerFunctions::setModifChanges( curr_target ) == 0) 
                    y2milestone("modified %1", curr_target);
                else 
                    y2error("%1 already modified", curr_target);
                // goto EditDialog() (initModify)
                ret = `edit;
                break;
            }
        }
    boolean empt = isempty((list)UI::QueryWidget(`server, `Items));
    UI::ChangeWidget(`edit, `Enabled, !empt);
    UI::ChangeWidget(`delete,  `Enabled, !empt);
    return ret;
    }

void initiSNS (string key) ``{
boolean ac=false;
string ip = "";
foreach(map<string, any> row, IscsiServerFunctions::getConfig()["iSNS"]:[], {
 if(row["KEY"]:""=="iSNSAccessControl") ac=(row["VALUE"]:"No"=="Yes") ? true : false;
 if(row["KEY"]:""=="iSNSServer") ip=row["VALUE"]:"";
});
UI::ChangeWidget(`isns_ac, `Value, ac);
UI::ChangeWidget(`isns_ip, `Value, ip);
}

boolean validateiSNS(string key, map event){
 string ip = (string)UI::QueryWidget(`isns_ip, `Value);
 boolean valid=true;
 if (!isempty(ip)) valid = IP::Check(ip);
	else valid= true;
 if (!valid)Popup::Error (_("Invalid IP address."));
 return valid;
}

void storeiSNS(string option_id, map option_map){
 string ip="";
 if ((boolean)UI::QueryWidget(`isns_ac, `Value))
  {
   ip = (string)UI::QueryWidget(`isns_ip, `Value);
  }
   string ac=((boolean)UI::QueryWidget(`isns_ac, `Value))?"Yes":"No";
   if (ip=="") ac="";
   IscsiServerFunctions::setiSNS(ip, ac);
}

//	**************** Edit Dialog	*****************************

// init values for modifying target (read it from stored map)
void initModify (string key) 
    {
    list <term> inc_items = [];
    y2milestone( "initModify %1 %2", curr_target, curr_tpg );
    UI::ChangeWidget( `id(`target), `Value, (splitstring(curr_target, ":"))[0]:"");
    UI::ChangeWidget( `id(`target),`Enabled, false);
    UI::ChangeWidget( `id(`identifier), `Value, (splitstring(curr_target, ":"))[1]:"");
    UI::ChangeWidget( `id(`identifier), `Enabled, false);
    UI::ChangeWidget( `id(`tpg), `Value, tostring(curr_tpg) );
    UI::ChangeWidget( `id(`tpg), `Enabled, false);
    string ipp = IscsiLioData::GetNetworkPortal( curr_target, curr_tpg )[0]:"";
    y2milestone( "initModify ipp:%1", ipp );
    UI::ChangeWidget( `id(`ipaddr), `Value, (splitstring(ipp, ":"))[0]:"");
    UI::ChangeWidget( `id(`ipaddr), `Enabled, true);
    UI::ChangeWidget( `id(`port), `Value, (splitstring(ipp, ":"))[1]:"3260");
    UI::ChangeWidget( `id(`port), `Enabled, true);
    UI::ChangeWidget( `id(`auth), `Value, IscsiLioData::GetTpgAuth(curr_target,curr_tpg));
    list<list> lun = IscsiLioData::GetLunList( curr_target, curr_tpg );
    y2milestone( "initModify lun:%1", lun );
    foreach( list l, lun,
        {
        inc_items = add( inc_items, `item(`id(size(inc_items)), l[0]:99, l[1]:"", l[2]:""));
        });
    UI::ChangeWidget(`id(`lun_table), `Items, inc_items);
    }

symbol handleModify(string key, map event)
    {
    if( event["EventReason"]:"" == "Activated" )
        {
        switch((symbol)event["WidgetID"]:nil)
            {
            case `delete:
                any del = UI::QueryWidget(`id (`lun_table),`CurrentItem);
                if (del != nil)
                    {
                    if(Popup::ContinueCancel(_("Really delete the selected item?")))
                        {
                        y2milestone("Delete LUN %1 from table", del);
                        list<term> it=(list<term>)UI::QueryWidget(`lun_table, `Items);
                        it = RemoveById( it, del );
                        UI::ChangeWidget(`id(`lun_table), `Items, it);
                        } 
                    else 
                        y2milestone("Delete canceled");
                    }
                break;
            case `edit:
                list<term> items = (list<term>)UI::QueryWidget(`lun_table, `Items);
                integer edit_pos=tointeger(UI::QueryWidget(`lun_table, `CurrentItem));
                y2milestone( "handleModify pos:%1 items:%2", edit_pos, items );
                term ret = LUNDetailDialog( edit_pos, items );
                y2milestone( "handleModify ret:%1", ret );
                if( ret!=`Empty() )
                    {
                    items[edit_pos]=ret;
                    UI::ChangeWidget(`lun_table, `Items, items);
                    UI::ChangeWidget(`lun_table, `CurrentItem, edit_pos);
                    }
                break;
            case `add:
                items = (list<term>)UI::QueryWidget(`lun_table, `Items);
                ret = LUNDetailDialog( -1, items );
                if( ret!=`Empty() )
                    {
                    items = (list<term>)add( items, ret ); 
                    UI::ChangeWidget(`lun_table, `Items, items);
                    UI::ChangeWidget(`lun_table, `CurrentItem, size(items)-1);
                    }
                break;
            }
        }
    boolean enab = !isempty((list)UI::QueryWidget(`lun_table, `Items));
    UI::ChangeWidget(`edit, `Enabled, enab);
    UI::ChangeWidget(`delete, `Enabled, enab);
    }

void storeModify(string option_id, map option_map)
    {
    boolean chg = false;
    if( !IscsiLioData::HasTarget( curr_target, curr_tpg ) )
        {
        chg = true;
        if( !IscsiLioData::AddTarget( curr_target, curr_tpg ))
            {
            string txt = sformat( _("Problem creating target %1 with tpg %2"), 
                                  curr_target, curr_tpg  );
            Popup::Error( txt );
            }
        }
    string ipp = IscsiLioData::GetNetworkPortal( curr_target, curr_tpg )[0]:"";
    string ip = (string)UI::QueryWidget(`ipaddr, `Value );
    integer port = tointeger(UI::QueryWidget(`port, `Value ));
    y2milestone( "storeModify ip:%1 port:%2 ipp:%3", ip, port, ipp );
    string np = sformat( "%1:%2", ip, port );
    if( !isempty(ip) && np != ipp )
        {
        chg = true;
        if( !IscsiLioData::SetNetworkPortal( curr_target, curr_tpg, np ))
            {
            string txt = sformat( _("Problem setting network portal to %1"), np  );
            Popup::Error( txt );
            }
        }
    list<term> it = (list<term>)UI::QueryWidget(`lun_table, `Items);
    list<integer> nll = maplist( term t, it, ``(tointeger(t[1]:"-1")));
    list<integer> oll = maplist( list lli, 
                                 IscsiLioData::GetLunList( curr_target, curr_tpg ),
                                 ``(lli[0]:-1));
    y2milestone( "storeModify oll:%1", oll );
    y2milestone( "storeModify nll:%1", nll );
    foreach( integer l, oll,
        {
        if( !contains( nll, l ))
            {
            chg = true;
            if( !IscsiLioData::DoRemoveLun( curr_target, curr_tpg, l ))
                {
                string txt = sformat( _("Problem removing lun %1"), l );
                Popup::Error( txt );
                }
            }
        });
    foreach(term row, it,
        {
        map lun = $[ "lun" : tointeger(row[1]:"-1"),
                     "nm" : row[2]:"",
                     "path" : row[3]:"" ];
        if(IscsiLioData::NeedUpdateLun( curr_target, curr_tpg, lun ))
            {
            y2milestone( "storeModify lun:%1", lun );
            chg = true;
            if( !IscsiLioData::DoUpdateLun( curr_target, curr_tpg, lun ))
                {
                string txt = sformat( _("Problem setting lun %1 (name:%2) to path %3"),
                                      lun["lun"]:-1, lun["nm"]:"", lun["path"]:"" );
                Popup::Error( txt );
                }
            }
        });
    boolean val = (boolean)UI::QueryWidget(`auth, `Value);
    if( val!=IscsiLioData::GetTpgAuth(curr_target,curr_tpg))
        {
        chg = true;
        if(!IscsiLioData::SetTpgAuth(curr_target,curr_tpg,val))
            {
            string txt = sformat( _("Problem setting auth on %1:%2 to %3"),
                                  curr_target, curr_tpg, val );
            Popup::Error( txt );
            }
        }
    if( chg )
        {
        IscsiLioData::UpdateConfig();
        initModify("");
        }
    }

//	************** Add Target Dialog	******************
// initialize function for create new target
void initAddTarget(string key)
    {
    // some proposed values
    string target = "iqn";
    string date = ((map<string, any>)SCR::Execute(.target.bash_output, "date +%Y-%m"))["stdout"]:"";
    string domain = ((map<string, any>)SCR::Execute(.target.bash_output, "dnsdomainname"))["stdout"]:"";
    string uuid = ((map<string, any>)SCR::Execute(.target.bash_output, "uuidgen"))["stdout"]:"";
    uuid = deletechars(uuid, "\n");
    if (!isempty(domain))
        {
        domain = (splitstring(domain, "\n"))[0]:"";
        list<string> tmp_list=splitstring(domain, ".");
        domain = sformat("%1.%2", tmp_list[1]:"", tmp_list[0]:"");
        } 
    else 
        domain="com.example";
    target = deletechars(sformat("%1.%2.%3", target, date, domain), "\n");
    y2milestone("init values for add_target %1", target);
    UI::ChangeWidget(`id(`target), `Value, target);
    UI::ChangeWidget(`id(`identifier), `Value, uuid);
    UI::ChangeWidget(`id(`tpg), `ValidChars, String::CDigit());
    UI::ChangeWidget(`id(`tpg), `Value, "1");
    list<term> ip = (list<term>)UI::QueryWidget(`id(`ipaddr), `Items);
    string s =ip[0,1]:"";
    y2milestone("initAddTarget ip:%1", s );
    UI::ChangeWidget(`id(`ipaddr), `Value, s);
    UI::ChangeWidget(`id(`port), `ValidChars, String::CDigit());
    UI::ChangeWidget(`id(`port), `Value, "3260");
    UI::ChangeWidget(`id(`auth), `Value, true);
    }

list uiTarget()
    {
    integer tpg = nil;
    tpg = tointeger(UI::QueryWidget(`id(`tpg),`Value));
    return( [ (string)UI::QueryWidget(`id(`target), `Value), 
              (string)UI::QueryWidget(`id(`identifier), `Value),
              tpg ] );
    }

void storeAddTarget(string option_id, map option_map)
    {
    list target = uiTarget();
    y2milestone( "storeAddTarget %1", target );
    curr_target = sformat( "%1:%2", target[0]:"", target[1]:"" );
    curr_tpg = target[2]:-1;
    storeModify(option_id,option_map);
    }


// validate function checks if target/tpg are unique and not empty
boolean validateAddTarget(string key, map event)
    {
    list target = uiTarget();
    y2milestone( "validateAddTarget %1", target );
    boolean ret = true;
    if( isempty(target[0]:"") )
        {
        Popup::Error(_("The target cannot be empty."));
        UI::SetFocus(`id(`target));
        ret = false;
        }
    else if( target[2]:nil==nil )
        {
        Popup::Error(_("The target portal group cannot be empty."));
        UI::SetFocus(`id(`tpg));
        ret = false;
        }
    else if( IscsiLioData::HasTarget(sformat( "%1:%2", target[0]:"", target[1]:"" ), target[2]:-1 ))
        {
        Popup::Error(_("The target already exists."));
        UI::SetFocus(`id(`target));
        ret = false;
        }
    return ret;
    }

string GetLunList( map<integer,integer> lmap )
    {
    string s = "";
    foreach( integer l1, integer l2, lmap,
        {
        if(!isempty(s))
            s = s + "-";
        s = s + sformat("%1:%2",l1,l2);
        });
    return( s );
    }

string GetAuthString( map am )
    {
    string ret = "";
    if( !isempty(am["incoming"]:[]) )
        ret = _("Incoming");
    if( !isempty(am["outgoing"]:[]) )
        {
        if(!isempty(ret))
            ret = ret + "/";
        ret = ret +  _("Outgoing");
        }
    if( isempty(ret) )
        ret = _("None");
    return( ret );
    }

void initClient(string key)
    {
    y2milestone( "initClient %1 %2", curr_target, curr_tpg );
    UI::ChangeWidget( `id(`target), `Value, (splitstring(curr_target, ":"))[0]:"");
    UI::ChangeWidget( `id(`target),`Enabled, false);
    UI::ChangeWidget( `id(`identifier), `Value, (splitstring(curr_target, ":"))[1]:"");
    UI::ChangeWidget( `id(`identifier), `Enabled, false);
    UI::ChangeWidget( `id(`tpg), `Value, tostring(curr_tpg) );
    UI::ChangeWidget( `id(`tpg), `Enabled, false);
    list<string> clnt = IscsiLioData::GetClntList( curr_target, curr_tpg );
    y2milestone( "initClient clnt:%1", clnt );
    list <term> inc_items = [];
    string auth = _("Disabled");
    boolean tgt_auth = IscsiLioData::GetTpgAuth( curr_target, curr_tpg );
    foreach( string s, clnt,
        {
        if( tgt_auth )
            {
            map m = IscsiLioData::GetAuth( curr_target, curr_tpg, s );
            auth = GetAuthString(m);
            }
        string lun = "";
        map<integer,integer> lmap = IscsiLioData::GetClntLun( curr_target, curr_tpg, s );
        string luns = GetLunList( IscsiLioData::GetClntLun( curr_target, curr_tpg, s ) );
        inc_items = add( inc_items, `item(`id(size(inc_items)), s, luns, auth));
        });
    UI::ChangeWidget(`id(`clnt_table), `Items, inc_items);
    }

symbol handleClient(string key, map event)
    {
    if( event["EventReason"]:"" == "Activated" )
        {
        switch((symbol)event["WidgetID"]:nil)
            {
            case `delete:
                any del = (integer)UI::QueryWidget(`id (`clnt_table),`CurrentItem);
                if (del != nil)
                    {
                    if(Popup::ContinueCancel(_("Really delete the selected item?")))
                        {
                        y2milestone("handleClient Delete Client %1 from table", del);
                        list<term> it=(list<term>)UI::QueryWidget(`clnt_table, `Items);
                        string clnt = GetById(it,del)[1]:"";
                        del_clnt = add( del_clnt, clnt );
                        if( haskey(changed_lun,clnt))
                            changed_lun = remove(changed_lun,clnt);
                        y2milestone( "handleClient del_clnt:%1", del_clnt );
                        it = RemoveById( it, del );
                        UI::ChangeWidget(`id(`clnt_table), `Items, it);
                        } 
                    else 
                        y2milestone("handleClient: Delete canceled");
                    }
                break;
            case `edit_lun:
                {
                integer edit_pos=tointeger(UI::QueryWidget(`clnt_table, `CurrentItem));
                list<term> items = (list<term>)UI::QueryWidget(`clnt_table, `Items);
                term it = items[edit_pos]:`Empty();
                string s  = it[1]:"";
                y2milestone( "handleClient pos:%1 clnt:%2", edit_pos, s );
                map<integer,integer> lm = LUNMapDialog( s );
                y2milestone( "handleClient lun_map:%1", lm );
                if( lm!=nil )
                    {
                    it[2] = GetLunList(lm);
                    items[edit_pos] = it;
                    UI::ChangeWidget(`clnt_table, `Items, items);
                    UI::ChangeWidget(`clnt_table, `CurrentItem, edit_pos);
                    }
                }
                break;
            case `edit_auth:
                {
                integer edit_pos=tointeger(UI::QueryWidget(`clnt_table, `CurrentItem));
                list<term> items = (list<term>)UI::QueryWidget(`clnt_table, `Items);
                term it = items[edit_pos]:`Empty();
                string s  = it[1]:"";
                y2milestone( "handleClient pos:%1 clnt:%2", edit_pos, s );
                map auth = ClntAuthDialog( s );
                y2milestone( "handleClient auth:%1", auth );
                if( auth!=nil )
                    {
                    it[3] = GetAuthString(auth);
                    items[edit_pos] = it;
                    UI::ChangeWidget(`clnt_table, `Items, items);
                    UI::ChangeWidget(`clnt_table, `CurrentItem, edit_pos);
                    }
                }
                break;
            case `add:
                /*
                items = (list<term>)UI::QueryWidget(`clnt_table, `Items);
                ret = LUNDetailDialog( -1, items );
                if( ret!=`Empty() )
                    {
                    items = (list<term>)add( items, ret ); 
                    UI::ChangeWidget(`clnt_table, `Items, items);
                    UI::ChangeWidget(`clnt_table, `CurrentItem, size(items)-1);
                    }
                */
                break;
            }
        }
    boolean enab = !isempty((list)UI::QueryWidget(`clnt_table, `Items));
    UI::ChangeWidget(`edit_lun, `Enabled, enab);
    UI::ChangeWidget(`delete, `Enabled, enab);
    UI::ChangeWidget(`copy, `Enabled, enab);
    enab = enab && IscsiLioData::GetTpgAuth( curr_target, curr_tpg );
    UI::ChangeWidget(`edit_auth, `Enabled, enab);
    }

boolean validateClient(string key, map event)
    {
    boolean ret = true;
    return( ret );
    }

void storeClient(string option_id, map option_map)
    {
    list target = uiTarget();
    y2milestone( "storeAddTarget %1", target );
    curr_target = sformat( "%1:%2", target[0]:"", target[1]:"" );
    curr_tpg = target[2]:-1;
    }

}
